Value ifname (\S+)
Value routerId ([0-9./]+)
Value state (\w+)
Value ipAddress ([0-9./]+)
Value vrf (\S+)
Value area ([0-9.]+)
Value areaStub ((\[Stub\])?)
Value isUnnumbered (UNNUMBERED)
Value networkType (\w+)
Value cost (\d+)
Value helloTime (\d+)
Value deadTime (\d+)
Value retxTime (\d+)
Value nbrCount (\d+)
Value passive (\w+)

Start
  ^\w+ is.*$$ -> Continue.Record
  ^${ifname} is ${state}.*$$
  ^\s+Internet Address ${ipAddress},.*Area ${area}\s?${areaStub}.*$$
  ^\s+This interface is ${isUnnumbered}, Area ${area}\s?${areaStub}.*$$
  ^\s+Router ID ${routerId}, Network Type ${networkType}, Cost:\s+${cost}.*$$
  ^\s+Timer intervals configured, Hello ${helloTime}.*, Dead ${deadTime}.*, Retransmit ${retxTime}.*$$
  ^\s+No Hellos \(${passive} interface\).*$$
  ^\s+Neighbor Count.*, Adjacent neighbor count is ${nbrCount}.*$$
